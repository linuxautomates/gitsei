plugins {
    id "org.sonarqube" version "3.0"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven'

    group 'io.levelops.server_api'
    version '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.13
    targetCompatibility = 1.13

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://artifactory.levelops.io/repository/maven-public/"
            credentials {
                username nexusUser
                password nexusPassword
            }
        }

        maven {
            url "https://packages.atlassian.com/maven-public/"
        }
    }

    dependencies {
        components.all {
            allVariants {
                withDependencies { deps ->
                    deps.each { dep ->
                        if (dep.group == 'io.levelops.commons') {
                            dep.version {
                                strictly levelopsCommonsVersion
                            }
                            dep.because ""
                        }
                    }
                }
            }
        }
    }

    test {
        exclude '**/*IntegrationTest*'
    }

}

subprojects {
    tasks.withType(Test) {
        maxParallelForks = 1
    }
}

dependencies {
    implementation project(':auth')
    implementation project(':server_api')
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

ext.levelopsCommonsVersion = "v0.1.9288-main"

ext.levelopsCommonsLib = { name -> [group: 'io.levelops.commons', name: name, version: levelopsCommonsVersion] }

ext.levelopsIntegrationsVersion = 'v0.1.825-main'

ext.levelopsIntegrationsLib = { name -> [group: 'io.levelops.integrations', name: name, version: levelopsIntegrationsVersion] }

ext.openApiVersion = '1.4.0'
ext.openapiLib = { component -> ["org.springdoc:springdoc-openapi-" + component + ":${openApiVersion}"] }

ext.libraries = [
        'timeseries'                  : [group: 'com.github.jrachiele', name: 'timeseries', version: '0.3'],
        'lombok'                      : [group: 'org.projectlombok', name: 'lombok', version: '1.18.22'],
        'junit'                       : [group: 'junit', name: 'junit', version: '4.13.2'],
        'assertj'                     : [group: 'org.assertj', name: 'assertj-core', version: '3.22.0'],
        'spring_security_test'        : [group: 'org.springframework.security', name: 'spring-security-test', version: '5.6.2'],
        'spring_boot_starter_test'    : [group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.6.6'],
        'spring_boot_cache'           : [group: 'org.springframework.boot', name: 'spring-boot-starter-cache', version: '2.6.6'],
        'spring_boot_starter_actuator': [group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: '2.6.6'],
        'okhttp'                      : [group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.3'],
        'mockito'                     : [group: 'org.mockito', name: 'mockito-core', 'version': '4.4.0'],
        'apache_commons'              : [group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'],
        'apache_collections'          : [group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'],
        'guava'                       : [group: 'com.google.guava', name: 'guava', version: '31.1-jre'],
        'totp'                        : [group: 'dev.samstevens.totp', name: 'totp', version: '1.7.1'],
        'totp_client'                 : [group: 'org.jboss.aerogear', name: 'aerogear-otp-java', version: '1.0.0.M1'],
        'csv'                         : [group: 'com.univocity', name: 'univocity-parsers', version: '2.9.1'],
        'log4j'                       : [group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'],
        'servlet'                     : [group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'],
        'gson'                        : [group: 'com.google.code.gson', name: 'gson', version: '2.9.0'],
        'jedis'                       : [group: 'redis.clients', name: 'jedis', version: '3.8.0'],
        'es'                          : [group: 'co.elastic.clients', name: 'elasticsearch-java', version: '8.1.1'],
        'embedded_postgres'           : [group: "io.zonky.test", name: "embedded-postgres", version: "1.3.1"],
        'micrometer_stackdriver'      : [group: "io.micrometer", name: "micrometer-registry-stackdriver", version: "1.9.7"],
        'gcp_logs'                    : [group: "com.google.cloud", name:"google-cloud-logging", version: "3.15.7"],
]
