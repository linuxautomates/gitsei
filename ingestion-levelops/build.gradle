
plugins {
    id 'com.github.jk1.dependency-license-report' version '1.17'
}

import com.github.jk1.license.render.*

licenseReport {
    renderers = [new XmlReportRenderer(), new CsvReportRenderer()]
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    // apply plugin: 'io.spring.dependency-management'

    group 'io.levelops'
    version '0.0.1-SNAPSHOT'

    if (System.getenv("VERSION") != null) {
        version = System.getenv("VERSION")
    }
    sourceCompatibility = 1.17
    targetCompatibility = 1.17

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url "https://artifactory.levelops.io/repository/maven-public/"
            credentials {
                username nexusUser
                password nexusPassword
            }
        }

        maven {
            url "https://packages.atlassian.com/maven-public/"
        }
    }

    dependencies {
        components.all {
            allVariants {
                withDependencies { deps ->
                    deps.each { dep ->
                        if (dep.group == 'io.levelops.commons') {
                            dep.version {
                                strictly levelopsCommonsVersion
                            }
                            dep.because "Use the commons versions specified in this project instead of any potential commons versions coming in the dependencies."
                        }
                    }
                }
            }
        }
    }

    test {
        exclude '**/*IntegrationTest*'
    }
}


wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

ext.levelopsCommonsVersion = 'v0.1.9245-main'
ext.levelopsCommonsLib = { name -> [group: 'io.levelops.commons', name: name, version: levelopsCommonsVersion] }

ext.levelopsIntegrationsVersion = 'v0.1.877-main'


ext.levelopsIntegrationsLib = {name -> [group: 'io.levelops.integrations', name: name, version: levelopsIntegrationsVersion]}

ext.openApiVersion = '1.4.0'
ext.openapiLib = { component -> ["org.springdoc:springdoc-openapi-" + component + ":${openApiVersion}"] }

ext.libraries = [
        'spring_boot_web'         : [group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.7.6'],
        'spring_jdbc'             : [group: 'org.springframework', name: 'spring-jdbc', version: '5.3.18'],
        'spring_actuator'         : [group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: '2.6.6'],
        'spring_boot_starter_test': [group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.6.6'],
        'spring_cache'            : [group: 'org.springframework.boot', name: 'spring-boot-starter-cache', version: '2.6.6'],
        'spring_data_redis'       : [group: 'org.springframework.data', name: 'spring-data-redis', version: '2.7.6'],
        'spring_redis'            : [group: 'org.springframework.integration', name: 'spring-integration-redis', version: '5.5.15'],
        'lombok'                  : [group: 'org.projectlombok', name: 'lombok', version: '1.18.26'],
        'junit'                   : [group: 'junit', name: 'junit', version: '4.13.2'],
        'assertj'                 : [group: 'org.assertj', name: 'assertj-core', version: '3.22.0'],
        'apache_commons'          : [group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'],
        'apache_collections'      : [group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'],
        'apache_http'             : [group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'],
        'guava'                   : [group: 'com.google.guava', name: 'guava', version: '31.1-jre'],
        'log4j'                   : [group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.2'],
        'okhttp'                  : [group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.3'],
        'jackson_core'            : [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.2.2'],
        'jackson_yaml'            : [group: 'com.fasterxml.jackson.dataformat', 'name': 'jackson-dataformat-yaml', 'version': '2.13.2'],
        'wiremock'                : [group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: '2.32.0'],
        'jsr'                     : [group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'],
        'mockito'                 : [group: 'org.mockito', name: 'mockito-core', 'version': '4.4.0'],
        'jooq'                    : [group: 'org.jooq', name: 'jooq', version: '3.16.5'],
        'embedded_postgres'       : [group: "io.zonky.test", name: "embedded-postgres", version: "1.3.1"],
        'hikari'                  : [group: 'com.zaxxer', name: 'HikariCP', version: '5.0.1'],
        'tomcat'                  : [group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '10.0.20'],
        'servlet'                 : [group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'],
        'kotlin_stdlib'           : [group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.6.20'],
        'gson'                    : [group: 'com.google.code.gson', name: 'gson', version: '2.9.0'],
        'google_oauth_client'     : [group: 'com.google.oauth-client', name: 'google-oauth-client', version: '1.33.2'],
        'google_client_jackson'   : [group: 'com.google.http-client', name: 'google-http-client-jackson2', version: '1.41.6'],
        'redis'                   : [group: 'redis.clients', name: 'jedis', version: '3.8.0']
]
